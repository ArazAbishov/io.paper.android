apply plugin: "pmd"

pmd {
    toolVersion = "5.5.2"
}

afterEvaluate {
    // ToDo: consider running pmd only for the main sourceSet
    android.applicationVariants.each { variant ->
        def pmdTask = tasks.create("pmd${variant.name.capitalize()}", Pmd)

        pmdTask.group = "verification"
        pmdTask.description = "Run Pmd for the ${variant.name}"

        // Fail early.
        pmdTask.ignoreFailures = false

        // Only html reports.
        pmdTask.reports {
            html.enabled true
            xml.enabled false
        }

        pmdTask.exclude(
                "**/R.java"
        )

        // see: https://pmd.github.io/pmd-5.5.2/pmd-java/rules/index.html
        pmdTask.ruleSets = [
                "java-android",
                "java-basic",
                "java-braces",
                "java-clone",
                "java-codesize",
                "java-coupling",
                "java-design",
                "java-empty",
                "java-imports",
                "java-logging-java",
                "java-junit",
                "java-optimizations",
                "java-sunsecure",
                "java-strings",
                "java-typeresolution",
                "java-unnecessary",
                "java-unusedcode"
        ]

        def compileTask = variant.javaCompile
        pmdTask.source = compileTask.source
        pmdTask.dependsOn(compileTask)

        def checkTask = tasks.getByName("check")
        checkTask.dependsOn(pmdTask)
    }
}