apply plugin: "checkstyle"

afterEvaluate {
    android.applicationVariants.each { variant ->
        def checkstyleTask = tasks.create("checkstyle${variant.name.capitalize()}", Checkstyle)

        checkstyleTask.group = "verification"
        checkstyleTask.description = "Run Checkstyle for the ${variant.name}"

        // Fail early.
        checkstyleTask.ignoreFailures = false

        // Do not output results into console.
        checkstyleTask.showViolations = false

        // Only html reports.
        checkstyleTask.reports {
            html.enabled = true
            xml.enabled = false
        }

        checkstyleTask.configFile = rootProject.file(
                "plugins/android-checkstyle-style.xml"
        )

        def compileTask = variant.javaCompile
        def checkTask = tasks.getByName("check")

        checkstyleTask.classpath = compileTask.classpath.plus(project.files(android.bootClasspath))
        checkstyleTask.source = compileTask.source.findAll {
            // ignore all directories with generated sources
            !it.absolutePath.find(".*generated.*")
        }

        checkstyleTask.dependsOn(compileTask)
        checkTask.dependsOn(checkstyleTask)
    }
}