apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "org.sonarqube"
apply plugin: "io.fabric"
apply plugin: "spoon"
apply from: rootProject.file("plugins/android-checkstyle.gradle")
apply from: rootProject.file("plugins/android-errorprone.gradle")
apply from: rootProject.file("plugins/android-paperwork.gradle")
apply from: rootProject.file("plugins/android-dexcount.gradle")
apply from: rootProject.file("plugins/android-findbugs.gradle")
apply from: rootProject.file("plugins/android-pmd.gradle")

def versions = rootProject.ext.versions
def libraries = rootProject.ext.libraries

repositories {
    // required by crashlytics
    maven {
        url("https://maven.fabric.io/public")
    }

    // used by butterknife
    maven {
        url("https://oss.sonatype.org/content/repositories/snapshots")
    }

    flatDir {
        dirs "libs"
    }
}

def getFabricKey = {
    def fabricPropertiesFile = file("fabric.properties")

    // retrieve api key from file, otherwise look for a key in environment
    if (fabricPropertiesFile.exists()) {
        Properties properties = new Properties()
        properties.load(fabricPropertiesFile.newDataInputStream())
        return properties.getProperty("apiKey")
    } else {
        return System.getenv("PAPER_FABRIC_KEY")
    }
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.tools

    defaultConfig {
        applicationId "io.paper.android"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.sdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        // jUnit test runner for android
        testInstrumentationRunner "io.paper.android.PaperInstrumentationTestsRunner"

        // Fabric API key
        manifestPlaceholders = [
                fabricKey: getFabricKey()
        ]

        // Make generated apks contain version name and code.
        archivesBaseName = "app-${versionName}-${versionCode}"
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true

        disable "GoogleAppIndexingWarning" // App does not have deep linking.
        warning "InvalidPackage" // Okio references java.nio that does not presented in Android SDK.
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        paper {
            storeFile file("paper.jks")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
    }

    packagingOptions {
        exclude "META-INF/rxjava.properties"
    }

    buildTypes {
        debug {
            // custom application suffix which allows to
            // install debug and release builds at the same time
            applicationIdSuffix = ".debug"

            // jacoco test coverage reports both for
            // androidTest and test source sets
            testCoverageEnabled = true

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false

            // Using paper.jks to sign debug build type.
            signingConfig = signingConfigs.paper
        }
        release {
            // enabling crashlytics for debug builds
            ext.enableCrashlytics = true
        }
    }
}

spoon {
    // for debug output
    debug = true

    // test coverage
    codeCoverage = true
}

dependencies {
    compile "hu.supercluster:paperwork:${libraries.paperwork}"

    // Support libraries
    compile "com.android.support:design:${libraries.support}"
    compile "com.android.support:appcompat-v7:${libraries.support}"
    compile "com.android.support:recyclerview-v7:${libraries.support}"
    compile "com.android.support:support-annotations:${libraries.support}"

    // Google dagger
    compile "com.google.dagger:dagger:${libraries.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${libraries.dagger}"

    // Google auto-value
    provided "com.google.auto.value:auto-value:${libraries.autovalue}"
    annotationProcessor "com.google.auto.value:auto-value:${libraries.autovalue}"

    // Auto-value extensions
    compile "com.gabrielittner.auto.value:auto-value-cursor-annotations:${libraries.autovaluecursor}"
    annotationProcessor "com.gabrielittner.auto.value:auto-value-cursor:${libraries.autovaluecursor}"

    // ReactiveX
    compile "io.reactivex.rxjava2:rxjava:${libraries.rxjava}"
    compile "io.reactivex.rxjava2:rxandroid:${libraries.rxandroid}"
    compile "com.github.akarnokd:rxjava2-interop:${libraries.rxjavainteroperop}"

    // Square libraries
    compile "com.squareup.sqlbrite:sqlbrite:${libraries.sqlbrite}"
    debugCompile "com.squareup.leakcanary:leakcanary-android:${libraries.leakcanary}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${libraries.leakcanary}"

    // Jake Wharton libraries
    compile "com.jakewharton.timber:timber:${libraries.timber}"
    compile "com.jakewharton:butterknife:${libraries.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${libraries.butterknife}"

    // Fabric tools
    compile("com.crashlytics.sdk.android:crashlytics:${libraries.crashlytics}@aar") {
        transitive = true;
    }

    // Test dependencies
    testCompile "junit:junit:${libraries.junit}"
    testCompile "org.assertj:assertj-core:${libraries.assertj}"
    testCompile "org.mockito:mockito-core:${libraries.mockito}"
    testCompile "nl.jqno.equalsverifier:equalsverifier:${libraries.equalsverifier}"

    // Android test dependencies
    androidTestCompile("com.google.truth:truth:${libraries.truth}") {
        exclude group: 'junit' // Android has JUnit built in.
    }

    // Firebase lib to capture screenshots
    androidTestCompile(name: "firebase", ext: "aar")

    // Spoon client which allows to capture screenshots
    androidTestCompile "com.squareup.spoon:spoon-client:${libraries.spoon}"

    androidTestCompile "com.android.support.test.espresso:espresso-core:${libraries.espresso}"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${libraries.espresso}") {
        // Causes IncompatibleClassChangeError
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
        exclude module: "appcompat-v7"
        exclude module: "support-v4"
        exclude module: "design"
    }
}
